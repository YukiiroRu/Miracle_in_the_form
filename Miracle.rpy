label Miracle:

    # $ _game_menu_screen = None #AUTHOR: Доделать внутри игровое меню.
    $ reload_names()
    $ backdrop = "prologue"
    $ prolog_time()
    $ new_chapter(-1, translation_new["prologue"])
    $ renpy.block_rollback()

# Сон.

    stop music fadeout 1

    $ renpy.pause(1, hard = True)

    scene anim prolog_1
    with fade2

    $ renpy.notify("Это ему, блин, снится.")

    play music music_list["drown"]

    window show
    "Мне опять снился этот сон: зачем он? \n{w}Каждую ночь одно и то же, уже на протяжении полугода."
    window hide

    scene bg ext_night_camp_entrance

    show owl:
        pos (948, 88) #AUTHOR: Позиция совы, на ПК - (948, 88), для смартфона (632, 61).

    show prologue_dream
    with fade2

    $ volume (0.2, "sound")
    play sound sfx_owl_far

    window show
    "Вижу ворота. Каждый раз одни, и те же ворота, и на них сидит эта странная птица. Что это, какой-то лагерь?.."
    "И этот сон повторяется снова и снова… Он так меня мучает."
    "…Этот страх… {b}Но не за себя, а за ту, кто со мной в этом сне…{/b}"
    "Да, именно за неё и уже потом за себя."
    "Кто она?.. Я не знаю. Но она каждый раз в этом сне."
    "Её нечёткие очертания, голос."
    "Он слышится гулким эхом, как будто заложило уши, но почему-то слова я понимаю…"
    "В них чувствуется волнение и страх. Но мне кажется, что она больше боится за меня…"
    "Она не похожа ни на кого из тех, кого я знаю, она особенная и я точно знаю, что никому не позволю причинить ей вред."
    "…"
    "С каждым днём ужас из этого сна проникает в меня всё сильней…"
    "Я знаю, что должен её защитить, во что бы то ни стало. Даже если понадобится отдать мою {b}{i}жизнь{/i}{/b} за неё…"
    "Это сводит меня с ума…"
    "Этот наводящий ужас сон, как надоедливая муха или как приговор суда без возможности подать протест или апелляцию."

    hide owl
    with slow

    "Птица исчезает, но раньше этого не было. Это новая странность моего сна."
    "Но главное то, что прогнать его я не могу и мне всё ясней кажется, что и не хочу…"
    "Стоит только закрыть глаза и вот он снова манит меня туда {w}неотвратимо {w}тихо, как в паутину жуткого паука."
    "{w=.1}.{w=.1}.{w=.1}."
    window hide

    scene bg int_catacombs_entrance:
        zoom 1.05 anchor(48,27)
        ease 0.20 pos(0, 0)
        ease 0.20 pos(25,25)
        ease 0.20 pos(0, 0)
        ease 0.20 pos(-25,25)
        repeat

    play sound2 sfx_intro_bus_stop_sigh

    show prologue_dream
    with flash

    play ambience ambience_catacombs_stones

    window show
    "Странный и необъяснимо жуткий коридор, мы бежим через него."
    "Но это не простой коридор, мне кажется, что это бомбоубежище или какой-то шахтёрский трак?"
    "Но где он находится?.. {w}Ни одного намёка, ни малейшего."
    "Нигде не видно никаких табличек с надписями или же чего-то подобного, что могло бы пролить свет на то, где мы?"
    "В голове звучит устрашающий шум, он не громкий, но от него в жилах холодеет кровь."
    "Мы бежим от какой-то {b}угрозы,{/b} которая вот-вот настигнет нас…"
    "Бежим и бежим, и кажется, что этому не будет конца."
    "Коридор сухой и гулкий. Везде, на полу, на стенах и даже на потолке, пыль и паутина."
    window hide

    scene bg int_mine_backdoor:
        zoom 1.05 anchor(48,27)
        ease 0.20 pos(0, 0)
        ease 0.20 pos(25,25)
        ease 0.20 pos(0, 0)
        ease 0.20 pos(-25,25)
        repeat

    show prologue_dream
    with fade

    window show
    "Бежим направо, налево и снова направо, и прямо… Сколько же раз я считал эти повороты?"
    "От этого бесконечного бега мне становится плохо, начинает тошнить."

    scene bg int_mine_crossroad:
        zoom 1.05 anchor(48,27)
        ease 0.20 pos(0, 0)
        ease 0.20 pos(25,25)
        ease 0.20 pos(0, 0)
        ease 0.20 pos(-25,25)
        repeat

    show prologue_dream
    with fade

    "Бежим и нам надо успеть, иначе точно конец."
    "…"
    my "Беги! Беги, не оглядывайся!"
    dreamgirl "{b}Нет,{/b} я не оставлю тебя одного!"
    my "Да беги же! Обо мне не думай! Беги, это {b}приказ!{/b}"
    "То, что гналось за нами, неумолимо приближалось. Я не могу понять, что это, но оно пугает меня."

    scene bg int_mine_halt_dynamite:
        zoom 1.05 anchor(48,27)
        ease 0.20 pos(0, 0)
        ease 0.20 pos(25,25)
        ease 0.20 pos(0, 0)
        ease 0.20 pos(-25,25)
        repeat

    show prologue_dream
    with fade

    "Почему оно нас преследует? Чего оно хочет?.."
    "И кого оно преследует? Меня или её? Или нас обоих?"
    "Может, хочет убить? Или что-то ещё?.."
    "…"
    dreamgirl "Но ты не справишься {b}один!{/b}"
    my "Беги, я справлюсь! {b}Верь мне!{/b}"
    window hide

    scene bg int_catacombs_door_no_light

    show prologue_dream
    with fade

    $ renpy.pause(0.2, hard=True)

    scene bg int_catacombs_door

    show prologue_dream
    with diss

    window show
    my "Скорей! Сюда!"
    my "Вот дверь, там есть другой выход, я знаю!"
    "Откуда я знал, что в этой комнате есть дверь, которая ведёт к выходу, мне не понятно. \n{w}Но я был в этом уверен."
    "Просто уверен и всё."
    "Как и в том, {w}что из этой комнаты, выйдет только один… Но я не был уверен, что этим {b}одним{/b} буду я."
    "Но если не я, то и оно тоже не выйдет из этой… {w}…этой… чем бы оно ни было."
    window hide

    stop ambience

    $ volume (0.15, "sound")
    play sound sfx_open_door_mines_metal

    $ renpy.pause(1, hard=True)

    scene bg int_catacombs_living_nodoor

    show prologue_dream
    with fade

    $ renpy.pause(1, hard=True)

    window show
    "В комнатке какая-то мебель, старые приборы, ещё ламповые, по стенам протянуты провода, есть освещение."
    "Освещение? Это вызывало чувство дежавю."
    my "Только осторожно, здесь всё очень старое…"
    my "Вот сюда, в эту дверь, скорей."
    dreamgirl "Мне страшно…"
    "Её голос звучал умоляюще — не оставлять её одну. Но я не мог этого себе позволить."
    my "Не бойся, беги."
    "Мои слова были непреклонны и она подчинилась."
    window hide

    scene bg int_catacombs_living_nodoor:
        pause 0.5
        block:
            linear 0.05 pos (-5,-5)
            linear 0.05 pos (0,0)
            linear 0.05 pos (5,5)
            linear 0.05 pos (0,5)
            linear 0.05 pos (5,0)
            linear 0.05 pos (0,0)
            repeat 8

    show prologue_dream

    $ volume (0.3, "sound")
    play sound sfx_nightmare_underground_rumble

    window show
    "Как только она проскочила в проём валяющейся на полу двери, прозвучал грохот обрушения. Я остался запертым в этом жутком месте, вместе с этой… {w=.1}«тварью»."
    "Но меня больше беспокоила её безопасность, а не моя."
    window hide

    $ renpy.pause(3, hard=True)

    window show
    my "Ты в порядке?!"
    dreamgirl "Да, в порядке! Я приведу помощь!"
    my "Нет, я же сказал, {b}беги отсюда и не возвращайся!{/b}"
    dreamgirl "Обещай, что с тобой всё будет хорошо!"
    window hide

    $ renpy.pause(1)

    menu:

        "Обещаю. Быстрей, уходи!":
            $ score_m += 1

        "Я сделаю всё, что смогу…":
            $ score_m -= 1

    window show
    "Каждый раз нужно что-то отвечать. Не ответить мне в голову не приходило."
    "Верный этот ответ или же нет?.. \nМожет, нужно было сделать что-то другое?"
    "Так не хочется расставаться с ней. Но каждый раз приходится, {w}по-другому никак."
    "В противном случае этот гадский сон может и не кончится, кто знает?"
    "Но после ответа всё обрывается. Сну конец, но не моим мукам в попытке разгадать его тайну."
    window hide

    stop music fadeout 2

# На работе.

    scene bg int_office_day
    with flash

    play ambience ambience_medium_crowd_outdoors

    window show
    "{w=.1}.{w=.1}.{w=.1}."
    "Сегодня у всех тяжёлый день."
    "В офисе между людьми как будто кошка пробежала. Хотя, почему именно кошка?"
    "Нет, скорее долбаный чёрт. Начальник рвёт и мечет. Вообще-то он почти всегда такой, но в этот раз уж вовсе вышел из себя."
    "Меня это не напугало, но стало действовать на нервы, и я стал то и дело посматривать на часы."
    th "Сколько же ещё до конца?.. Да япона мать…"
    "Хотя работой в последнее время я себя не слишком утруждал, скорее делал вид, что работаю. Но и эти потуги выбивали из меня последние силы."
    "Время шло медленно, а находиться здесь и именно сейчас мне становилось всё противней."
    "Конечно, я не мог просто взять и уйти. Я же не начальник и не мог позволить себе такую роскошь."
    window hide

    $ set_mode_nvl()
    nvl show dissolve

    th "И как только я вообще тут оказался? Я ведь не хотел работать в офисе."
    th "Нужны были деньги, вот и пришлось идти сюда работать. А так, разве я сюда пошёл бы?!"
    "Я с детства мечтал путешествовать, искать то, что другие ещё не нашли."
    "Мечтал найти инопланетную тарелку, ну, или хотя бы проникнуть в зону 51 в Америке. О ней я узнал впервые из какой-то телепередачи."
    "Я много читал различных журналов, смотрел передачи, связанные с этой темой по телевизору и в интернете."
    "Надеялся узнать как можно больше информации, но она была отрывочна и во многих случаях ложна или же попросту ошибочна. \nА кто-то хотел таким образом прославиться."
    "На свете много разных чудиков или, как таких ещё называли, фриков, готовых на всё, что угодно."
    "Родителям такое моё хобби не слишком-то нравилось, но в принципе они не возражали. Лишь бы не пошёл по кривой дорожке."
    "Папа покупал мне журналы и даже постепенно сам увлёкся этой темой."
    "Мы любили путешествия, семейные походы, но когда я предлагал папе поискать что-то необычное. Он лишь отмахивался, говоря, \nчто нам, скорее всего, ничего интересного не попадётся."
    nvl clear
    "До одного момента, когда, будучи в одном из очередных походов, он увидел в небе что-то странное."
    "Тот день запомнился нам надолго и был хитом всех папиных рассказов, которыми он любил удивлять друзей, \nкогда те оставались у нас с ночёвкой."
    "Мы с папой были как неразлей вода, общие хобби, похожие взгляды на жизнь, на семью и многое другое, это очень нас сближало."
    "Мама же всегда была душой компании, она всегда умела всех развеселить и очень вкусно и сытно накормить."
    "Поэтому в нашем доме всегда было много народа, постоянно кто-то ночевал, то и дело мы ходили на шашлык в лес, даже зимой."
    "…"
    "Зимние походы в лес, это было очень весело, лыжные гонки с призами из сладостей, шуточные снежные битвы, катание с горки на санках и многое другое."
    "Мама любила гулять на лыжах и нас к этому пристрастила. \nА вот мы с папой больше любили велосипедные прогулки, походы в лес с ночёвкой и т.д. \n{w}Одним словом, мы с папой были всегда за движение."
    nvl clear
    "Хотя я был не прочь и полениться. Да только с моими родителями было не до лени."
    "В общем, было весело, но как только родителей не стало, всё изменилось. Гостей не стало, веселье ушло, как уходит мимолётное вдохновение."
    "Мне не повезло, верных и надёжных друзей я за мои недолгие годы не нажил. Я всегда был замкнут и стеснителен с посторонними людьми, это мне сильно мешало."
    "Да и в моём окружении не было тех, с кем мне хотелось бы завести настоящую дружбу. {w}Не по-ве-зло…"
    "…"
    "Я остался совсем один, и долгое время искал, чем же мне заняться."
    "Работал на разных работах, но ту работу, на которой хотел бы задержаться хотя бы на год, так и не нашёл."
    "И как-то раз повстречал приятеля, с которым учился в школе, и он предложил похлопотать за меня в той компании, в которой работал."
    "Другого варианта не было. Я стал работать, но эта работа тоже не доставляла мне удовольствия."
    "Надо было что-то менять, и я решил уйти и уехать в другое место, может, даже в другую страну."
    "А что, люди везде работают, может, и мне повезёт, думал я."

    nvl hide dissolve
    $ set_mode_adv()

    $ persistent.sprite_time = "day"

    show blink_m

    window show
    "Кто-то несильно ударил меня по плечу, выведя меня из раздумий."
    window hide

    play sound sfx_punch_medium

    hide blink_m
    show unblink_m

    window show
    th "Да кто это?! Так и кондратий хватить может от неожиданности."
    show alx normal at center with dissolve
    "Передо мной материализовался с дружелюбной улыбкой на лице мой, так сказать, приятель Александр. Но ему это имя не нравилось и он предпочитал, чтобы его звали Алексом."
    alx "Привет! Ты как? Выглядишь помятым."
    th "Да неужели? Да ты прям ясновидящий."
    "По моему виду было несложно определить, что со мной что-то не так."
    my "Да нормально, спал плохо, вот и всё."
    alx "Вот только не говори, что опять этот твой сон. Я больше и слышать об этой ерунде не хочу."
    my "Так ты и не слушай."
    show alx smile at center
    alx "Так я и не буду."
    my "Ладно-ладно, молчу, сегодня все что-то прямо как с цепи сорвались."
    show alx serious at center
    alx "Ага, и не говори. Слышал, что произошло утром?"
    my "Нет, а что?"
    th "Хотя мне, если честно, до фонаря, что там случилось."
    alx "Да Вадим… пришёл весь какой-то как не в себе, то есть какой-то странный…"
    th "Странный?.. Хм."
    "…"
    my "Ну, как видишь, не только он сегодня такой, ну, вернее, день сегодня какой-то такой…"
    "Было сложно подобрать правильное логическое объяснение тому, что происходило, и я не стал развивать эту тему дальше."
    alx "Это да. Так тебе дальше рассказывать или нет?"
    my "А, ну да, извини."
    alx "Так вот, он явился на работу вот такой и стал кидаться на всех. А затем наехал на генерального, прикинь… Генеральный его только из-за контракта с немцами не уволил, он же его ведёт."
    my "И что?"
    show alx normal at center
    alx "Да ничего, генеральный его только что домой отправил, и всё. \nСказал, что таким не хочет его видеть на рабочем месте."
    alx "Ну да, от него же зависит контракт…"
    "Алекс сделал скептическое выражение лица, говоря тем самым: „Ага, прям только от него“."
    alx "А может, из-за его мамаши, она ведь с ним знакома, ну-у, ты понимаешь."
    th "От него зависит контракт?! Да неужели?! Да если б не мы с ребятами, видал бы он этот контракт как свои уши, или ещё что, поинтересней."
    my "Ага, само собой, понимаю. Сам-то как, Алекс?"
    alx "Да нормально, как всегда. О да, вот, ты просил меня сделать тебе копию той программы, я записал на флешку."
    alx "Правда, я мог перепутать флешки, утром спешил. Если что, ты мне позвони, и я тебе её принесу. Ok?"
    my "Ага, хорошо."
    alx "Ладно, я побежал, а то наш гегемон идёт, увидит здесь без дела, уволит ещё нафиг."
    show alx smile at center
    my "Ага, беги. Смотри, как зыркает, того и гляди спалит глазами, как «Змей-Горыныч»."
    "И мы, стараясь, чтоб на нас не обращали внимания, ухмыльнулись."
    hide alx
    with moveoutright
    "Хоть генеральный у нас ещё тот, но он был для нас неисчерпаемым источником различного рода подколов и шуток в его сторону."
    "День шёл медленно, как будто нарочно тянулся, причиняя муки и выматывая меня вконец."
    "Но вот и конец дня, завтра выходные, плюс я написал заявление на пару отгулов, обосновав это тем, что работа вне офиса будет продуктивней."
    th "Наконец-то хоть отдохну как следует."
    my "Да-аа… если получится."
    "Сказал я себе и удивился: „Почему я это сказал?“"

    stop ambience

    window hide

# Дома.

    $ volume (0.25, "sound_loop2")
    play sound_loop2 sfx_Tictak

    play music music_list["farewell_to_the_past_edit"] fadein 2

    scene bg int_semen_room_1
    with dissolve

    window show
    "Вечер. Уютная комната, освещённая настольной лампой и тёплый лунный свет, проникающий через распахнутые шторы."
    "Я вернулся домой, ещё более уставший и помятый, чем был днём на работе."
    "Всё же то напряжение, царившее днём, измотало меня сильнее, чем я думал."
    "Настроение у меня было скверным и от этого было ещё хреновей. Я переоделся и пошёл в ванную, чтобы привести себя в порядок после дня."
    window hide

    with fade

    window show
    "Вернувшись из ванной и войдя в комнату, я поймал себя на мысли, что она всё такая же, как и с утра."
    th "Ну да, верно, а какой она должна быть? У меня же никого нет, даже встретить некому."
    "Животных я не заводил потому, что не мог уделять им время. Да и не хотел, в квартире животному всё же хуже, чем на природе."
    window hide

    scene bg int_semen_room_1
    with dissolve

    window show
    "В комнате лишь я, монитор и тихое тиканье часов."
    my "Так-сс… пойти перекусить, что ли?"

    $ volume (2.0, "sound2")
    play sound2 sfx_stomach_growl

    "В животе одобрительно заурчало."
    "Днём раньше я сделал свою домашнюю пиццу, мама научила и я очень любил её делать. А что, вкусно и сытно."
    window hide

    stop sound_loop2

    scene bg int_semen_kitchen
    with diss

    window show
    "Я пошёл на кухню, поставил чайник на плиту, достал пиццу из холодильника и засунул её в микроволновку."
    "Вид из окна был приятный, но чего-то в нём не хватало."
    th "Да-аа, луны здесь не увидишь. А сегодня она необычайно хороша."
    window hide

    play sound sfx_angry_ulyana

    $ renpy.pause(7, hard=True)

    window show
    "Чайник вскипел быстро и вот из чашки уже приятно пахло кофе с молоком."
    "Пицца разогретая пахла тоже очень вкусно и я с удовольствием налетел на неё, как будто пробовал пиццу впервые."
    "Поев и помыв после себя посуду, я вернулся обратно в мой маленький мирок — одиночки."
    window hide

    play sound_loop2 sfx_Tictak

    scene bg int_semen_room_1
    with dissolve

    $ renpy.pause(1, hard=True)

    scene bg int_semen_room_2
    with dissolve

    $ renpy.pause(1, hard=True)

    scene bg int_semen_room_3
    with dissolve

    $ renpy.pause(3, hard=True)

    scene bg monitor1
    with fade

    window show
    "В комнате было тепло и уютно, свет луны придавал ей лёгкую дымку таинственности."
    "В слегка улучшенном настроении я сел на стул перед компьютерным столом и закрыл глаза."
    window hide

    show blink

    window show
    "Все дела я переделал, для меня одного дел в квартире не так уж много."
    th "Можно посвятить несколько часов жизни работе над проектом."
    th "Эх… так не хочется, но в тоже время нужно…"
    th "А уж потом отдых по полной программе."
    "Мама и папа с детства меня приучали к работе по дому. И всегда говорили: „{b}Настоящий мужчина должен уметь делать если не всё, {w}{i}то многое{/i}{/b}“."
    "…"
    "Иногда меня заставляли делать то, чего мне не хотелось."
    "Теперь же, спустя несколько лет после того, как родителей не стало, я понял, как мне повезло с ними. {w}И все их уроки пошли мне на пользу."
    th "Эх… надо начинать, проект мы так и не закончили, а сам себя он не сделает."
    window hide

    hide blink
    show unblink

    play sound sfx_wind_gust

    window show
    "Мне почудилось дуновение ветра."
    th "Странно, форточка закрыта, может быть, просто почудилось? Странный сегодня день."
    "…"
    th "Так хочется тишины и покоя… {w}Ладно, надо поработать, хочется или нет."
    "К концу месяца началась запара, как всегда, перед сдачей документов на утверждение начальству."
    th "Может, в консоль сыгрануть чуток? А уж потом заняться проектом?"
    my "О, мне же надо программу, что мне Алекс записал, установить."
    my "Так, хорошо, сначала дело, потом вся ночь впереди, успею ещё погонять читаков."
    "Погонять читеров было у меня чем-то вроде хобби."

    scene cgs monitor_02
    with dissolve

    $ volume (1.0, "sound_loop")
    play sound_loop sfx_sex1

    th "Что?! Это кто?.. Подружка Алекса… голая, и он сам собственной персоной."
    th "Хотя, чего от него можно было ждать? Он же раздолбай. Да и потом, он предупреждал, что мог перепутать флешки."
    th "Вот же дурень. Я его точно прибью, он же мог рабочую флешку перепутать и сунуть своё home video начальнику. Вместо проекта, вот бы он удивился такому зрелищу."
    th "И зачем он только это всё записывал?"
    th "Это хорошо, что флешка с таким содержимым оказалась у меня. \nА если б у чужака, то сейчас эта запись гуляла бы на просторах интернета."

    stop sound_loop

    my "Нет, всё, хватит с меня."
    window hide

    window show

    scene bg monitor1
    with fade

    "{w=.1}.{w=.1}.{w=.1}."
    my "Я этого придурка точно когда-нибудь прибью."
    my "Блин, где же файл проекта? Ещё не хватало его потерять."
    window hide

    scene bg monitor
    with fade

    window show
    my "Фух, вот он…"
    my "Ну, ничего, я тебя сделаю."
    my "Не сделаешь! Ха, ха, ха, ха-аа…"
    my "Сделаю, сделаю."
    my "По ходу, начинаю сходить с ума. Сам с собой разговариваю."
    "{w=.1}.{w=.1}.{w=.1}."
    window hide

    play sound sfx_keyboard_mouse

    $ renpy.pause(3, hard=True)

    window show
    "Работа закипела, хоть и не с таким энтузиазмом, как раньше, но всё же она сдвинулась с мёртвой точки."
    th "Ну, ещё немного и всё."
    my "Отлично, всё! И за сколько я справился? Около часа прошло, не плохо, думал, что буду возиться с этим дольше."
    my "Окай, теперь можно и оторваться по-полной."
    my "Ну, читаки, берегитесь, античит идёт по ваши души."
    window hide

    $ renpy.pause(3, hard=True)

    stop music fadeout 2

    scene bg int_semen_room_4
    with dissolve

    play sound sfx_home_phone_ring

    window show
    "Через минуту, как я произнёс эти слова, зазвонил телефон."
    window hide

    $ renpy.pause(2, hard=True)

    play music music_list["just_think"] fadein 2

    window show
    my "Да кто это ещё?"
    "Я взял трубку, {w}но ничего не услышал."
    my "Алло?.. Алло!.. Кто там?.. Эй, что за шуточки?!"
    "Но ответа не последовало, только какая-то зловещая тишина."
    "Я резко положил трубку на аппарат. Этот звонок мне показался странным и неприятным."
    my "Да блин, что ещё за хрень?!"
    "И тут я услышал голос с улицы, зовущий меня по имени."
    voice "Эй, Семён!"
    my "А-аа!.. Что за чёрт? Это ещё кто?"
    my "Может, Алекс пришёл за флешкой? Хотя, чего ему орать с улицы? Мог же просто взять и позвонить. Или, если пришёл, подняться ко мне и взять флешку без ора со двора."
    window hide

    scene bg semen_room_window
    with dissolve

    window show
    "Я выглянул в окно, но никого не увидел. Двор был пуст. И выглядел как-то необычно."
    th "Интересно, почему во дворе некого нет? Ведь скоро новый год, обычно в эти дни, даже вечером, во дворе полно детей, которые катаются на коньках в футбольной коробке."
    "Её каждый год заливал наш дворник дядя Саня, и по совместительству массовик-затейник."
    my "Кто меня звал? Хм… там ведь некого нет. Нет, точно, походу надо в отпуск, так и мозги свернуть можно. Мне ещё не хватало похода к мозгоправу."
    "{w=.1}.{w=.1}.{w=.1}."
    window hide

    scene bg int_semen_room_4
    with dissolve

    window show
    "Тут я почувствовал, как какой-то дискомфорт постепенно наполняет меня, как вода, которая наполняет чашу и вот-вот выльется через край."
    my "Да что такое? Нет, всё, возьми себя в руки, это тебе просто показалось. Просто, показалось."
    "Я старался говорить это спокойным твёрдым голосом."
    "Но такая попытка убрать то неприятное чувство, что появилось так внезапно, не принесла большого успеха."
    my "Ну, всё, хватит уже. Чтоб вас всех. Отвалите нафиг, кто бы вы ни были!"
    "Только усилие воли наконец-то отогнало это наваждение."
    th "Возьми себя в руки, ты же не девчонка сопливая, а мужик, в конце-то концов."

    stop music fadeout 3

    "…"
    my "Ой, пофиг. Ладно, глянем, что там в игровом чате."
    "Я сел за компьютер пообщаться с моими виртуальными друзьями. Хоть такие, но всё же друзья."
    window hide

    play sound sfx_Purr

    scene bg monitor1_chat
    with dissolve

    play sound sfx_Purr

    $ renpy.pause(2, hard=True)

    scene bg monitor2_chat
    with diss

    $ renpy.pause(2, hard=True)

    scene bg monitor3_chat
    with diss

    play sound sfx_Purr

    $ renpy.pause(2, hard=True)

    scene bg monitor4_chat
    with diss

    window show
    "Просидев в чате полчаса, я почувствовал, что устал и решил, что наверно мне лучше будет всё же прилечь."
    window hide

    stop music fadeout 2

    pause(3)

    scene anim prolog_4
    with fade

    window show

    show blinking_m

    "Я лёг и уставился в потолок, пытаясь заснуть, но почему-то сон не приходил, даже никакого намёка на него."
    th "Вот ведь, только думал, что сейчас усну и ни в одном глазу."
    "Я с самого детства засыпал в течение пяти минут, а тут лежал и тупо пялился в потолок."
    "Вокруг всё стихло, я лежал, пытаясь понять то, что со мной происходит. Не в этот вечер, я пытался осмыслить всю свою жизнь, от момента, когда стал себя осознавать, до этого дня."
    show so head at left with dissolve:
        alpha 0.5
    "Воспоминания лились как из рога изобилия, вот мама и папа, а вот и моя первая любовь, которой не суждено было сбыться…"
    "В это мгновение в голову вкралась мысль, что недавно появившаяся в соседней квартире девушка очень похожа на мою первую любовь."
    hide so
    with diss
    th "Да, ведь верно, эта девушка очень на неё похожа…"
    th "Удивительного тут, конечно, не много. Ведь на самом деле, люди находят своих двойников, похожих на них как близнецы."
    "И всё же это сходство не давало мне покоя."
    window hide

    menu:

        "Продолжать лежать.":
            $ score_m -= 1
            jump p_c

        "Пойти пройтись.":
            $ score_m += 1
            jump p_c

        "Сесть за компьютер.":
            jump Miracle_bad

label p_c:

    if score_m == 3:
        jump Miracle_good
    else:
        jump Miracle_end

label Miracle_good:

    window show
    "Сон никак не наступал. Часы тикали и тикали, было спокойно и тепло."
    "Я продолжал лежать, словно меня кто-то держал в сознании, не давая уснуть и мучая воспоминаниями."
    th "Нет, так я точно не усну. Чтоб его через…"
    "Я не вслух ругнулся, но от этого не уснёшь."
    my "Пойти пройтись что-ли, подышу свежим воздухом, тем более, погода вроде хорошая, может, тогда и уснуть будет легче."
    "Уверенности, что прогулка мне чем-то поможет, не было, но я принял решение и не собирался его менять."
    window hide

    scene bg int_semen_room_1
    with dissolve

    window show
    "Я встал, оделся и решил пойти в магазин, купить что-нибудь вкусненькое."
    "Мне всегда становилось легче, когда у меня было что-то, чем можно себя развлечь, будь это еда или же что-то ещё."
    th "Что-то стало холодать, не пора ли нам поддать? Что-то ветер дует в спину, не пора ли к магазину?"
    "…"
    my "Так. Где телефон? {w}А, вот он."
    window hide

    play sound sfx_home_phone_take

    window show
    my "Ключи."

    stop sound_loop2 fadeout 2

    play sound sfx_keys_rattle

    scene bg int_stairwell
    with fade

    play sound sfx_open_door_2

# Пошёл гулять.

    "Я взял телефон, ключи и вышел на лестничную клетку."
    "Было тихо, мне это показалось странным, обычно здесь бывает чуть громче. Хотя так, может, и лучше. А то и так везде шум да гам, отдыха от этого шумового кошмара почти нет."
    "А эта тишина была даже приятна."
    "На клетке было чисто, даже чересчур: может быть, чище, чем у меня в квартире."
    "Я обратил на это внимание только потому, что смотрел себе под ноги. В последнее время на улице было очень скользко и смотреть себе под ноги вошло у меня в привычку."
    window hide

    $ persistent.sprite_time = "night"
    scene bg ext_new_year
    with dspr

    play ambience ambience_cold_wind_loop

    window show
    "Я вышел на улицу и огляделся по сторонам."
    th "Да… вот и ёлку дядя Саня уже нарядил."
    th "А что удивительного, ведь новый год уже почти на пороге."
    "От этих мыслей стало чуть теплее на душе."
    "Я с детства любил наряжать ёлку, сначала это была живая ель, а уж потом мы с папой купили искусственную, чему я был рад — очень жаль было выкидывать бедную ёлочку."
    "Во дворе всегда ставили живую ёлку, но с этим я ничего поделать не мог."
    my "Здравствуйте, дядь Сань!"
    show ds smile hand at center with dissolve
    ds "Я бульдозерист!"
    "Ответил он, указуя пальцем в небо."
    "Что бы это значило? Не понятно."
    "В своё время дядя Саня был и вправду бульдозеристом, и когда был слегка пьян, то отвечал так всем, кто с ним здоровался. Но это было, когда он был навеселе, и звучало это очень забавно."
    my "Ага, я знаю. Спасибо за ёлочку, дядь Сань!"
    show ds normal at center with dspr
    ds "Ёлочка, ёлочка, в праздничный час, каждой иголочкой радавает нас… радавает на-а-ас…"
    "В ответ пропел дядя Саня с улыбкой на лице."
    hide ds
    with diss
    "Дядя Саня не был пьяницей, он жил один после смерти жены, детей у него было двое, но жили они отдельно. Вот он и мог иногда позволить себе расслабиться."
    "Была у дяди Сани ещё одна особенность, он выглядел моложе, чем ему было на самом деле, как ему это удавалось, загадка."
    ds "Йо-хо-хо и бутылка гнома!.."
    "Прозвучало у меня за спиной. Было понятно, что дядя Саня ещё кого-то встретил."
    th "Вообще-то, «йо-хо-хо и бутылка рома», {w}но не суть."
    "Дядя Саня был очень приятным человеком, добрым и неунывающим. Этим-то он мне и нравился, да и не только мне, его любили абсолютно все. Бывают же такие люди, которых любят просто так…"
    window show

    $ renpy.pause(1, hard=True)

    scene anim intro_3
    with dissolve

    window hide
    th "Интересно, а магазин-то ещё открыт? Да вроде он работает круглосуточно, с пятницы и все выходные. {w}Очень хочется мороженого с фисташками."
    "Я невольно улыбнулся, вспомнив детство, и как мы с родителями едим мороженое в парке культуры и отдыха. И как из-за того, что я загляделся на один из аттракционов, моё мороженное стащила ворона, прямо у меня из руки. Видимо, очень ей захотелось освежиться в тот жаркий день."
    "Но я на неё не в обиде. Я всегда любил животных. Хоть мы и не могли завести хотя бы морскую свинку или крыску, уж о кошке с собакой и говорить не приходилось."
    "У мамы была аллергия и поэтому о домашнем питомце я мог только мечтать."
    "И тут такой забавный конфуз."
    "Я шёл обычным и знакомым маршрутом, но в этот раз было иначе."
    th "Может, пройти через парк? Так и путь сокращу."
    "Решение пришло вдруг-внезапно, как будто мне его кто-то вложил в голову."
    window hide

    $ volume (0.5, "sound_loop3")
    play sound_loop3 sfx_steps_in_snow

    scene bg ext_winter_park_night_light
    with dissolve

    window show
    "В парке оказалось непривычно пусто. Это было странно."
    th "Интересно, а где люди? В парке обычно в это время всегда довольно много народа, тем более, новый год скоро, а тут…"
    "Но разбираться в этом вопросе мне было лень. И я прошёл парк поскорей."
    window hide

    stop sound_loop3

    scene bg int_shop_night
    with fade

    stop ambience

    play music music_list["gentle_predator"]

    window show
    "На моё удовольствие магазин был открыт и почти безлюден."
    "Внутри играла музыка, новогодней её не назовёшь, но хоть не грустная."
    th "И нафига они включают музыку? Так-то устаёш от шума ещё и тут нет от него отдыха."
    "Задерживаться в магазине я не стал, быстро нашёл, что хотел и направился к кассе."
    "Было не много странно, скоро новый год, а магазин не украшен."
    th "Хоть бы ёлочку маленькую, где поставили. {w=.1}Долбаные торгаши."
    mg "Пакет нужен? Или может, что-то ещё?.."
    my "Нет спасибо, больше не чего не нужно."
    mg "Может пиво по акции?.."
    my "Я же сказал: нет… Спасибо."
    "Её назойливость стала меня раздражать."
    mg "Хорошо. Спасибо за покупку, приходите ещё."
    "Как мне показалось, продавщица была не в меру жизнерадостная. Наверно была в предвкушении новогодних праздников и наверно ждала подарки от родных."
    "А там, кто знает?.."
    "Кроме мороженного, ничего другого я брать не стал, да и денег с собой оказалось не так много, на что-то ещё, мне всё равно бы не хватило."
    th "Эта кассирша… раньше я её вроде не видел. Впрочем, чему я удивляюсь?"

    stop music fadeout 3

    "Кассиры в магазинах меняются как по мановению волшебной палочки, раз и новая или новый, так что удивляться особо не чему."
    window hide

    scene anim intro_3
    with fade

    play ambience ambience_cold_wind_loop
    play sound sfx_intro_bus_stop_steps

    $ renpy.pause(1, hard=True)

    window show
    "Выйдя из магазина, я распаковал одно мороженное и пошёл по улице, наслаждаясь этим с детства любимым лакомством."

    $ renpy.pause(1, hard=True)

    scene anim intro_4
    with dissolve

    "Снега было не слишком много, так что идти было легко и даже приятно. Он похрустывал под ногами, разносясь непротяжным эхом."
    window hide

    scene anim intro_5
    with dissolve

    $ renpy.pause(1, hard=True)

    scene anim intro_6
    with dissolve

    window show
    "Я неспеша шёл вперёд. Наслаждаясь погодой и тишиной, погружённый в свои мысли, и даже не заметил, что оказался рядом с автобусной остановкой."
    window hide

    scene bg bus_stop
    with fade

    window show
    "Это была остановка автобуса 410, который ходил по окраине нашего городка и практически не заезжал в центр."
    th "Может, не идти обратно пешком?"
    "Меня накрыло чувство, что я довольно прилично устал."
    th "Ну, конечно, сидячий образ жизни, {b}да и работы,{/b} на здоровье влияют не слишком-то благотворно. Плюс постоянные стрессы, вот тебе и усталость."
    "Я сел на лавку и достал из пакета ещё одно мороженное, шоколадное."
    th "А, это с миндальной крошкой и с карамельным сиропом, вкусняшка."
    "Я сидел, ел и вспоминал, как мы с родителями проходили эту остановку."
    "Я вспомнил, как видел уезжающих с неё ребят."
    "Как впоследствии мне мама рассказывала, что только с этой остановки было отправление школьников в пионерский лагерь."
    "Мы с родителями проходили мимо, а на остановке стоял автобус. Тоже 410, из него вышел, видимо, вожатый в пионерской форме и, пока ребята заходили в автобус, он о чём-то разговаривал с родителями."
    "Сам я до пионера не дорос и был лишь октябрёнком. И смотрел на них с интересом и предвкушением, что и сам тоже в будущем поеду в пионерский лагерь."
    "Можно сказать, что стать пионером было моим заветным желанием, хотя бы тогда в детстве."
    "…"
    "Находясь в своих мыслях, я не заметил, что вокруг стало как-то необычно тихо."
    "Как будто я находился не в городе, а где-нибудь в тихом пригороде."
    window hide

    $ set_mode_nvl()
    nvl show dissolve

    voice "Кх… Кх…"
    "{w=.1}.{w=.1}.{w=.1}."
    "Тут я услышал негромкий кашель, как мне показалось, похожий на женский."
    "И следом же такой же негромкий вопрос, который был произнесён мягко и даже нежно:"
    voice "„Простите, пожалуйста. Скажите, а автобус 410 когда будет?“"
    "Нечего странного или необычного не подозревая, я посмотрел на наручные часы и ответил:"
    my "„Он, вообще-то, должен уже быть, но что-то его нет“."
    "Говоря это, я повернулся в сторону, откуда слышался голос."
    nvl clear

    play sound2 sfx_intro_bus_stop_sigh

    my "Да что… {w}за чёрт?! Эй, вы где?! Да что тут творится?!"
    "Вокруг никого не было. Когда я пришёл к остановке, пошёл несильный снег и припорошил мои следы. И не увидеть свежих следов я бы, при всём желании, не смог."
    th "Всё это очень странно. Женский голос…"
    "Он мне показался знакомым, но я никак не мог вспомнить, кого он мне напоминает?"
    "Я посмотрел ещё раз вокруг остановки, может, я не увидел где-то следов и может, этот голос был не так вот, из неоткуда, а это был живой человек."
    th "А что если это голос привидения или типа того, а?"
    "От этого предположения меня пробрал холод."
    "Это был холод не от мороза, который стал в эту минуту сильней, а от осознания, что вот сейчас со мной могло произойти всё что угодно."
    my "Да что ж за день сегодня такой?! Какая-то бредятина творится."
    "Вблизи послышался грубоватый с хрипотцой звук старого мотора."

    nvl hide dissolve
    $ set_mode_adv()

    play sound sfx_intro_bus_engine_start

    $ renpy.pause(1, hard=True)

    play sound_loop sfx_intro_bus_engine_loop fadein 3

    scene anim intro_9
    with fade2

    stop ambience

    window show
    "Автобус подъехал к остановке. И своим шумом привёл меня в более-менее нормальное состояние. Я встал передним и уставился на двери в ожидании."
    "Всё вокруг казалось каким-то иным. И автобус тоже."
    "{w=.1}.{w=.1}.{w=.1}."
    "Он выглядел как обычный автобус. Табличка с номером 410 красовалась у него за стеклом, но что-то мне показалось странным."
    "…{w}Хотя… может, просто показалось?.."
    window hide

    $ renpy.pause(2, hard=True)

# В автобусе.

    scene anim intro_10
    with fade

    play sound sfx_intro_bus_door_open

    $ renpy.pause(3, hard=True)

    scene anim intro_11
    with fade

    $ renpy.pause(3, hard=True)

    stop sound_loop fadeout 4

    scene anim intro_13
    with fade2

    $ renpy.pause(3, hard=True)

    scene bg int_liaz_bus_night
    with fade

    stop ambience fadeout 2

    play sound_loop sfx_bus_interior_moving fadein 4

    $ volume(0.5, "music")
    play music music_list["lightness_radio_bus"] fadein 7

    window show
    "Двери открылись и я вошёл в автобус. {w}Внутри было тепло, из радио лилась какая-то приятная музыка."
    "Автобус тронулся с места и мы, слегка покачиваясь, поехали вперёд по ночному городу."
    "Водитель, по-видимому, торопился, потому что жестом показал мне проходить. Разглядеть мне его не удалось, так как внутри было довольно темно. Я прошёл по автобусу в конец и сел у окна справа."
    window hide

    scene bg intro_xx
    with fade

    window show
    "В автобусе, кроме меня и водителя, никого не было. Так как времени было уже много и мало кто поедет куда-то на ночь глядя."
    "Правда, в предновогодние дни, пустой автобус это было не обычно. Но я не придал этому большого значения."
    "{w=.1}.{w=.1}.{w=.1}."
    "Автобус ехал не быстро, так как дорога была скользкая из-за гололёда. Но всё же уверенно двигался вперёд."
    "Он ехал, а я смотрел в окно и думал о своём надоедливом сне. Ну и, конечно же, о том голосе, что спросил меня:"
    "…"
    voice "„Простите, пожалуйста. Скажите, а автобус 410 когда будет?“"
    "Ощущение, что этот голос мне знаком, не отставало от меня."
    "Но я никак не мог понять, откуда он мне мог быть знаком?"
    th "Где же я слышал этот голос? Ну, где?! Он такой знакомый и приятный… {w}да ё-маё, бывает же такое, вертится где-то рядом, а поймать не можешь."
    "Глядя на улицы, по которым двигался автобус, я видел людей, парочки, которые идут и улыбаются. Люди идут, кто домой, кто куда-то ещё."
    "Мне стало грустно, что я один и что, вернувшись домой, меня никто не встретит."
    th "Жаль, что всё вот так идёт, {w}по-дурацки."
    "Я представил себя кем-то другим, может, учёным, или же, как я мечтал в юности, искателем приключений."
    "И не понимал, почему же всё пошло именно так, а не иначе? Почему я так и не стал тем, кем мечтал быть?"
    "В голове завертелись мысли, которые я не мог отогнать, они возникли вот так вдруг, как айсберг посреди ночного океана."
    th "Как так получилось, что я один и никому оказался не нужен? Вроде зла я никому не делал, так за что судьба меня так кинула?"
    th "Что я делал не так?.."
    th "И как мне теперь быть дальше?"
    "От обиды на жизнь, что поступает с людьми так несправедливо, из глаз потекли слёзы."
    "Да, наверно, плакать в моём возрасте выглядит стыдным, но я не мог сдержаться, да и всё равно никто не видел."
    "Автобус ехал, слегка покачиваясь, и я почувствовал, что начинаю засыпать."
    "Слёзы вытянули из меня последние силы."
    "То ли от тепла, а может, от того, что я обессилил, сон начал меня всё сильней заманивать в мир греческого бога сновидений „Морфея“."
    "Сил сопротивляться этому у меня не осталось, да и желания тоже."
    "Да и мысли, что автобус может проехать мою остановку, у меня не возникло."
    "И так, {w}под звук мерно работающего мотора и музыку, что лилась как тёплый летний дождь, согревая и успокаивая, я провалился в тёплый бархатный сон."
    window hide

    stop music fadeout 3

    play music music_list["sparkles"]

    show blink
    with dissolve

    $ renpy.pause(1.5)

    stop sound_loop fadeout 2

    scene bg black

    $ renpy.pause(1, hard=True)

    show credits my_credits:
        xalign 0.5
        ypos 1.3
        linear 52.0 ypos -4.0

    $ renpy.pause(20, hard=True)

    stop music fadeout 2

    jump Miracle_day1

# ср, 09/марта/2022., 08:01 PM., Закончен.
